/*
 * Copyright (C) 2012-2015 The Android Money Manager Ex Project Team
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

allprojects {
    repositories {
        jcenter()
        // MPChartAndroid,
        maven { url "https://jitpack.io" }
    }
    // Force all dependent libs to use the same version of the support annotations.
    // This is used in testing libraries (Espresso).
    // https://code.google.com/p/android-test-kit/issues/detail?id=136
//    configurations.all {
//        resolutionStrategy.force 'com.android.support:support-annotations:22.2.1'
//    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.money.manager.ex"
        multiDexEnabled true
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 677
        versionName '2.15.0-beta1'
        // 601 = 1.4.17, stable

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //jniLibs.srcDirs = ['libs']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def version = "-" + defaultConfig.versionName.replace(".", "_") + "_r" + defaultConfig.versionCode
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace(".apk", version + ".apk"))
                }
            }
        }
    }

    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.github.gabrielemariotti.changeloglib:changelog:2.0.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.afollestad:material-dialogs:0.7.6.0'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.github.pedrovgs:lynx:1.5'
    compile 'com.nispok:snackbar:2.10.10'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    // Page indicator on tutorial. https://android-arsenal.com/details/1/1268
    compile 'me.relex:circleindicator:1.1.5@aar'
    // androidSVG
    compile project(':androidSVG_12')
    compile 'com.shamanland:fonticon:0.1.8'
    // Ref: https://developer.android.com/tools/building/multidex.html
    compile 'com.android.support:multidex:1.0.1'
    // solution for #391
    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'

    // Unit testing dependencies
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
//    // UI testing
//    androidTestCompile 'com.android.support.test:runner:0.3'
//    // Set this dependency to use JUnit 4 rules
//    androidTestCompile 'com.android.support.test:rules:0.3'
//    // Set this dependency to build and run Espresso tests
//    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
//    // add this for intent mocking support
//    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2'
//    // add this for webview testing support
//    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2'
//    // Set this dependency to build and run UI Automator tests
//    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
}

