/*
 * Copyright (C) 2012-2018 The Android Money Manager Ex Project Team
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 3
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // robolectric
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

ext {
    compileSdk = 33
    targetSdk = 31
    buildToolsVersion = '30.0.2'
    supportLibrary = '1.0.0'
    ext.kotlin_version = '1.9.0'

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    supportDependencies = [
            core             : "androidx.core:core:1.9.0",
            cardView         : "androidx.cardview:cardview:${supportLibrary}",
            design           : "com.google.android.material:material:1.8.0",
            preference       : "androidx.preference:preference:1.2.1",
            recyclerView     : "androidx.recyclerview:recyclerview:1.3.0",
            annotation       : "androidx.annotation:annotation:1.5.0",
            appcompat        : "androidx.appcompat:appcompat:1.6.1",
            sqlite           : "androidx.sqlite:sqlite:2.3.1",
            gridLayout       : "androidx.gridlayout:gridlayout:${supportLibrary}"
    ]
}

// This applies to the projects underneath.
allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    /**
     * Force all dependent libs to use the same version of the libraries.
     * This is used in testing libraries (Espresso).
     * https://code.google.com/p/android-test-kit/issues/detail?id=136
     */
    configurations.configureEach {
        resolutionStrategy {
            force 'org.hamcrest:hamcrest-core:1.3'
            force 'junit:junit:4.12'
            force supportDependencies.annotation
            force supportDependencies.design
            force 'com.google.code.gson:gson:2.8.2'
            force 'com.google.guava:guava:27.0.1-android'
            force 'com.google.code.findbugs:jsr305:3.0.1'
            force 'com.android.support:multidex:2.0.0'
        }
        // removes warnings about commons-logging on build.
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    //
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }
}
